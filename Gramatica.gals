<ACAO> ::= <CRIA> | <INCLUIR> | <SELECIONAR> | <ELIMINAR> | <DESCREVER> | <SETAR_BANCO> 
<CRIA> ::= CREATE <CRIA2>
<CRIA2> ::= DATABASE id #51 | TABLE id #38 ( <ATRIBUTOS> ) #58
<ATRIBUTOS> ::= <ATRIBUTO> <RATRIBUTOS> 
<ATRIBUTO> ::= id #33 <TIPO> #53 <RESTRICOES> 
<RATRIBUTOS> ::= , <RATRIBUTOS2> | ε
<RATRIBUTOS2> ::= <ATRIBUTOS> | <RESTRICAO_FINAL> 
<TIPO> ::= number ( numero <DECIMAL> ) | varchar ( numero ) | date | char ( numero ) 
<RESTRICOES> ::= CONSTRAINT <RESTRICAO1> | <RESTRICAO2> | ε
<RESTRICAO1> ::= id #15 <TIPO_RESTRICAO1> #55
<TIPO_RESTRICAO1> ::= null |  not null |  PRIMARY KEY | REFERENCES id #38 (<LISTAIDS>) 
<RESTRICAO2> ::= <TIPO_RESTRICAO2> #55
<TIPO_RESTRICAO2> ::= null |  not null | PRIMARY KEY |  REFERENCES id #38 (id #13) 
<RESTRICAO_FINAL> ::= CONSTRAINT <RESTRICAO3> <RRESTRICAO_FINAL> 
<RRESTRICAO_FINAL> ::= , <RESTRICAO_FINAL> | ε
<RESTRICAO3> ::= id #15 <TIPO_RESTRICAO3> #55
<TIPO_RESTRICAO3> ::= PRIMARY KEY ( <LISTAIDS> ) |  FOREIGN KEY ( <LISTAIDS> ) REFERENCES id #38 ( <LISTAIDS> )
<DECIMAL> ::= , numero | ε
<LISTAIDS> ::= id #13 <RLISTAIDS> | id #13
<RLISTAIDS> ::= , <LISTAIDS> | ε
<INCLUIR> ::= INSERT INTO id #18 <COLUNAS> VALUES ( <LISTAVALORES> ) 
<COLUNAS> ::= ( <LISTAIDS> ) | ε
<LISTAVALORES> ::= <VALOR> #43 <RLISTAVALORES> 
<VALOR> ::= numero | literal | data | ‘null’ 
<RLISTAVALORES> ::= , <LISTAVALORES> | ε
<SELECIONAR> ::= SELECT <CAMPOS> FROM <TABELAS> <CLAUSULA_WHERE> 
<CAMPOS> ::= <CAMPO> <RCAMPOS> | * 
<RCAMPOS> ::= , <CAMPOS> | ε
<CAMPO> ::= id <CAMPO2> #13
<CAMPO2> ::= .id | ε
<TABELAS> ::= <TABELA> <RTABELAS> 
<RTABELAS> ::= , <TABELAS> | ε
<TABELA> ::= id #18
<CLAUSULA_WHERE> ::= WHERE <CONDICOES> | ε
<CONDICOES> ::= <CONDICAO> <RCONDICAO> 
<CONDICAO> ::= <CAMPO> <OPERADOR_REL> <VALOR> #43 
<RCONDICAO> ::= <OPERADOR_LOG> <CONDICOES> | ε
<OPERADOR_REL> ::= = | > | < | >= | <= | <> 
<OPERADOR_LOG> ::= AND | OR 
<ELIMINAR> ::= DROP TABLE id #61 ;
<DESCREVER> ::= DESCRIBE id #63 ;
<SETAR_BANCO> ::= SET DATABASE id #65 ;
