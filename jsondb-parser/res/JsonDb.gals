#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = br.furb.jsondb.parser.core
ScannerCaseSensitive = false
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
letra: [a-zA-Z]
digito: [0-9]
nao_zero: [1-9]
inteiro: {digito}+
id: _? {letra} (_ | {letra} | {digito})*

#Tokens
// 2014.10@FURB.BR
// Felipe Loose Corso
// Janaína Carraro Mendonça Lima
// William Leander Seefeld

id: {id} | ' {id} '
numero: {inteiro} (\.{inteiro})?
literal: \" ([^\\ \" \n] | \\ (\\ | \"))* \"
data: {digito} {digito} / {digito} {digito} / {digito} {digito} {digito} {digito}

// Símbolos especiais
";"
"("
")"
","
"*"
"."
"="
">"
"<"
">="
"<="
"<>"

// Palavras reservadas
CREATE = id: "CREATE"
DATABASE = id: "DATABASE"
TABLE = id: "TABLE"
NUMBER = id: "NUMBER"
VARCHAR = id: "VARCHAR"
DATE = id: "DATE"
CHAR = id: "CHAR"
CONSTRAINT = id: "CONSTRAINT"
NULL = id: "NULL"
NOT = id: "NOT"
PRIMARY = id: "PRIMARY"
KEY = id: "KEY"
REFERENCES = id: "REFERENCES"
FOREIGN = id: "FOREIGN"
INSERT = id: "INSERT"
INTO = id: "INTO"
VALUES = id: "VALUES"
SELECT = id: "SELECT"
FROM = id: "FROM"
WHERE = id: "WHERE"
AND = id: "AND"
OR = id: "OR"
DROP = id: "DROP"
DESCRIBE = id: "DESCRIBE"
SET = id: "SET"
INDEX = id: "INDEX"

// Símbolos ignorados
:[\r\s\n\t]

#NonTerminals
<sentenca>
<acao>
<criar>
<criar_estrutura>
<lista_atributos>
<lista_atributos_rec>
<lista_atributos_rec_dec>
<atributo>
<tipo>
<decimal>
<restricao>
<nome_restricao_opt>
<restricao_constraint>
<lista_restricoes_finais_rec>
<lista_restricoes_finais>
<tipo_restricao_final>
<lista_ids>
<lista_ids_rec>
<incluir>
<colunas>
<lista_valores>
<valor>
<lista_valores_rec>
<selecionar>
<campos>
<lista_campos>
<lista_campos_rec>
<campo>
<tabela_opt>
<clausula_where>
<lista_condicoes>
<lista_condicoes_rec>
<condicao>
<op_relacional>
<op_logico>
<eliminar>
<descrever>
<index>
<setar_banco>
<eliminar_estrutura>

#Grammar
<sentenca> ::= <acao> ";" #99;
<acao> ::= <criar> | <incluir> | <selecionar> | <eliminar> | <descrever> | <setar_banco>;
<criar> ::= CREATE <criar_estrutura>;
<criar_estrutura> ::= DATABASE id #51 | TABLE id #52 "(" <lista_atributos> ")" | INDEX #66 <index>;
<lista_atributos> ::= <atributo> <lista_atributos_rec>;
<lista_atributos_rec> ::= "," <lista_atributos_rec_dec> | î;
<lista_atributos_rec_dec> ::= <atributo> <lista_atributos_rec> | <lista_restricoes_finais>;
<atributo> ::= id #17 <tipo> #53 <restricao>;
<tipo> ::= NUMBER #01 "(" numero #09 <decimal> ")" | VARCHAR #02 "(" numero #09 ")" | DATE #03 | CHAR #04 "(" numero #09 ")";
<decimal> ::= "," numero #08 | î;
<restricao> ::= <nome_restricao_opt> <restricao_constraint> #55 | î;
<nome_restricao_opt> ::= CONSTRAINT id #15 | î;
<restricao_constraint> 	::= NULL #20 | NOT NULL #21 | PRIMARY KEY #22 | REFERENCES #23 id #13 "(" #10 id #12 #29 ")";
<lista_restricoes_finais_rec> ::= "," <lista_restricoes_finais> | î;
<lista_restricoes_finais> ::= CONSTRAINT id #16 <tipo_restricao_final> #55 <lista_restricoes_finais_rec>;
<tipo_restricao_final> ::= PRIMARY KEY #22 "(" #10 <lista_ids> #26 ")" | FOREIGN KEY #24 "(" #10 <lista_ids> #28 ")" REFERENCES id #13 "(" #10 <lista_ids> #29 ")";
<lista_ids> ::= id #12 <lista_ids_rec>;
<lista_ids_rec> ::= "," <lista_ids> | î;
<incluir> ::= INSERT INTO id #13 <colunas> VALUES "(" #38 <lista_valores> #39 ")";
<colunas> ::= "(" #10 <lista_ids> #11 ")" | î;
<lista_valores> ::= <valor> <lista_valores_rec>;
<valor> ::= numero #32 | literal #33 | data #34 | NULL #20;
<lista_valores_rec> ::= "," <lista_valores> | î;
<selecionar> ::= SELECT <campos> #18 FROM #10 <lista_ids> #19 <clausula_where>;
<campos> ::= <lista_campos> | "*";
<lista_campos> ::= <campo> <lista_campos_rec>;
<lista_campos_rec> ::= "," <lista_campos> | î;
<campo> ::= id #12 <tabela_opt>;
<tabela_opt> ::= "." id #14 | î;
<clausula_where> ::= WHERE <lista_condicoes> | î;
<lista_condicoes> ::= <condicao> <lista_condicoes_rec>;
<lista_condicoes_rec> ::= <op_logico> #31 <lista_condicoes> | î;
<condicao> ::= <campo> <op_relacional> #30 <valor>;
<op_relacional> ::= "=" | ">" | "<" | ">=" | "<=" | "<>";
<op_logico> ::= AND | OR;
<eliminar> ::= DROP <eliminar_estrutura>;
<eliminar_estrutura> ::= TABLE id #61 | INDEX <index> #67;
<descrever> ::= DESCRIBE id #63;
<setar_banco> ::= SET DATABASE id #65;
<index> ::= id #13 "(" id #12 ")";
